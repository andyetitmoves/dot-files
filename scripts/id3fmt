#!/usr/bin/perl
# Pretty prints the output of `id3info' using ansi color escapes.

use strict;
use warnings;

my $ch = "\e[1;35m";
my $ca = "\e[1;37m";
my $cl = "\e[1;32m";
my $c0 = "\e[0m";

while (<>) {
    if (/^\*\*\* Tag information for (.*)$/) {
	print $ch.'Filename:'.$c0." $1\n";
	last;
    }
    next if /^\s+$/;
    print;
}

SYNC: {
    while (<>) {
	my $line = $_;
	last if /^\*\*\* mp3 info$/;
	do { print "\t$line"; next; } unless
	    /^=== ([A-Z0-9]{4,4}) \(([^:]+)\): (?:(?:(?:\((.*)\)(?:\[(.*)\])?)|(?:\[(.*)\])): )?(.*)$/;
	next if ($1 eq 'APIC');
	my $data = $6;
	if ($1 eq 'COMM' && $3 ne 'Noname') {
	    my $name = $3;
	    $name = 'Comment' if ($3 =~ /^\s*$/);
	    print "$ch$name";
	} else {
	    print "$ch$2";
	    my $needCode = 0;
	    if (defined ($3) && $3 ne '') {
		print " $ca($3)";
		print ' '.$cl."[$4]" if (defined ($4) && $4 ne '');
		$needCode = 1;
	    } elsif (defined ($5) && $5 ne '') {
		print ' '.$cl."[$5]";
		$needCode = 1;
	    }
	    print "$ch" if $needCode;
	}
	print ':';
	print (($1 eq 'USLT') ? "$c0\n\t" : "$c0 ");
	if ($1 eq 'IPLS') {
	    my $valid = 0;
	    my $key = '';
	    print "\n";
	    foreach (split (/\s*,\s*/, $data)) {
		if ($valid == 0) {
		    if ($_ =~ /^\s*$/) {
			$valid = 2;
		    } else {
			$key = $_;
			$valid = 1;
		    }
		} else {
		    print "\t$ca$key:$c0 $_\n"
			if ($valid != 2 && !($_ =~ /^\s*$/));
		    $valid = 0;
		}
	    }
	} else {
	    print "$data\n";
	}
    }

    while (<>) {
	if (/^\*\*\* Tag information for (.*)$/) {
	    print $ca.('-'x80).$c0."\n\n";
	    print $ch.'Filename:'.$c0." $1\n";
	    redo SYNC;
	}
	chomp;
	print (/^([^:]*):\s*(.*)$/ ? "$ch$1:$c0 $2\n" : "$ch$_$c0\n");
    }
}
