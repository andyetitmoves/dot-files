#!/bin/zsh

local file nfile ext tag value eof
typeset -a etags args
typeset -A exif

emulate -L zsh
setopt extendedglob
unsetopt bgnice monitor

etags=(DateTime DateTimeOriginal DateTimeDigitized \
    SubSecTime SubSecTimeOriginal SubSecTimeDigitized)

args=('%[EXIF:'$^etags']' '%i')
args=(-format "${(F)args}")

# Canonicalize filenames lest ImageMagick does "smart" filename parsing

for file in "$@"; do
    args+=`readlink -ef $file`
done

if coproc identify "${args[@]}"; then
    # Don't print useless messages when run in interactive shells
    disown
fi

while true; do
    eof=
    exif=()
    for tag in $etags; do
	if ! read -r -p value; then
	    eof=yes
	    break
	fi
	if [[ -n $value ]] && [[ $value != unknown ]]; then
	    [[ $tag == DateTime* ]] && [[ $value == ([:0 ])# ]] && continue
	    exif[$tag]="$value"
	fi
    done
    [[ $eof == yes ]] && break
    read -r -p file || break
    value="${exif[DateTime]:-${exif[DateTimeOriginal]:-${exif[DateTimeDigitized]}}}"
    if [[ -n $value ]] && [[ -n $file ]]; then
	ext="${exif[SubSecTime]:-${exif[SubSecTimeOriginal]:-${exif[SubSecTimeDigitized]}}}"
	[[ -n $ext ]] && value="$value.$ext"
	ext="${file:e}"
	[[ -n $ext ]] && ext=".$ext"
	value="${(j,:,)${(s, ,)value}}$ext"
	nfile="${file:h}/$value"
	if [[ $file != $nfile ]]; then
	    echo ${file:h}: ${file:t} '->' $value
	    mv -i $file $nfile
	fi
    else
	echo No date information found for file $file >&2
    fi
done
