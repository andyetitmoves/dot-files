#!/bin/zsh

local file nfile fmtpieces ext tag value eof
typeset -a etags fmtpieces
typeset -A exif

emulate -L zsh
setopt extendedglob

etags=(DateTime DateTimeOriginal DateTimeDigitized \
    SubSecTime SubSecTimeOriginal SubSecTimeDigitized)

fmtpieces=('%[EXIF:'$^etags']' '%i')

coproc identify -format "${(F)fmtpieces}" "./$^@"

while true; do
    eof=
    exif=()
    for tag in $etags; do
	if ! read -r -p value; then
	    eof=yes
	    break
	fi
	if [[ -n $value ]] && [[ $value != unknown ]]; then
	    [[ $tag == DateTime* ]] && [[ $value == ([:0 ])# ]] && continue
	    exif[$tag]="$value"
	fi
    done
    [[ $eof == yes ]] && break
    read -r -p file || break
    value="${exif[DateTime]:-${exif[DateTimeOriginal]:-${exif[DateTimeDigitized]}}}"
    if [[ -n $value ]] && [[ -n $file ]]; then
	ext="${exif[SubSecTime]:-${exif[SubSecTimeOriginal]:-${exif[SubSecTimeDigitized]}}}"
	[[ -n $ext ]] && value="$value.$ext"
	ext="${file:e}"
	[[ -n $ext ]] && ext=".$ext"
	nfile="${file:h}/${(j,:,)${(s, ,)value}}$ext"
	if [[ $file != $nfile ]]; then
	    echo $file '->' $nfile
	    mv -i $file $nfile
	fi
    else
	echo No date information found for file $file >&2
    fi
done
